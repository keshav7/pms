apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

group 'com.ekart.transport'

//snapshot version
version '1.0.4.9-SNAPSHOT'

//release version
//version '1.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/milestone' }
    maven { url 'http://repo.spring.io/snapshot' }
    maven { url 'https://repository.jboss.org/nexus/content/repositories/releases' }
    maven { url 'http://repo.maven.apache.org/maven2' }

    //ART
    maven { url 'http://10.85.59.116/artifactory/v1.0/artifacts/libs-snapshots-local' }
    maven { url 'http://10.85.59.116/artifactory/v1.0/artifacts/libs-release-local' }
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

configurations {
    querydslapt
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

idea {
    module {
        sourceDirs += file('src/main/generated')
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (version.endsWith("-SNAPSHOT")){
                repository(url: "http://10.85.59.116/artifactory/v1.0/artifacts/libs-snapshots-local")
            }
            else{
                repository(url: "http://10.85.59.116/artifactory/v1.0/artifacts/libs-release-local")
            }
        }
    }
}

ext['hibernate.version'] = '4.3.11.Final'


dependencies {

    compile project(':common-datatypes')

    // Spring Boot Related
    compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-loader-tools', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jersey', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: spring_boot_version
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: spring_boot_version) {
        exclude module: 'spring-boot-starter-tomcat'
    }

    //state Machine Squirrel
    compile group:'org.squirrelframework', name :'squirrel-foundation', version:'0.3.8'

    // Jackson Related
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-json-org', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hppc', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate4', version: jackson_version
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jackson_version
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jackson_version

    // Metrics
    compile group: 'com.codahale.metrics', name: 'metrics-annotation', version: '3.0.2'
    compile group: 'com.codahale.metrics', name: 'metrics-core', version: '3.0.2'
    compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-annotation', version: dropwizard_metrics_version
    compile group: 'com.ryantenney.metrics', name: 'metrics-spring', version: '3.1.3'

    // Lombok
    compile group: 'org.projectlombok', name: 'lombok', version: lombok_version

    // Joda Time
    compile group: 'joda-time', name: 'joda-time', version: joda_time_version
    compile group: 'joda-time', name: 'joda-time-hibernate', version: joda_time_hibernate_version
    compile group: 'org.jadira.usertype', name: 'usertype.core', version: usertype_core_version

    // Apache
    compile group: 'commons-io', name: 'commons-io', version: commons_io_version
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2.1'

    // Gson
    compile group: 'com.google.code.gson', name: 'gson', version: '1.7.2'

    // Swagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: springfox_version
    compile(group: 'io.springfox', name: 'springfox-swagger2', version: springfox_version) {
        exclude module: 'mapstruct'
        exclude module: 'com.fasterxml.jackson.core'
    }

    // AspectJ
    compile group: 'org.aspectj', name:'aspectjweaver', version: aspectj_version

    // QueryDsl
    compile "com.querydsl:querydsl-jpa:4.0.4"
    querydslapt "com.querydsl:querydsl-apt:4.0.3:jpa"

    // Hikari CP
    compile(group: 'com.zaxxer', name: 'HikariCP', version: HikariCP_version) {
        exclude(module: 'tools')
    }

    // Turbo Relayer
    compile('com.flipkart.restbus.hibernate:hibernate-restbus-client:2.0.8-Turbo') {
        exclude(group: "org.slf4j", module: "slf4j-log4j12")
        exclude group: 'javassist', module: 'javassist'
        exclude group: 'org.hibernate'
        exclude group: 'org.hibernate.javax.persistence', module: 'hibernate-jpa-2.0-api'
    }

    //Config Service Client Wrapper
    compile group: 'com.flipkart.cp', name: 'config-service-client-wrapper', version: '2.5.0-SNAPSHOT'

    //auditing
    compile 'org.hibernate:hibernate-envers:4.3.11.Final'

    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'

    //runtime("com.h2database:h2")
    runtime group: 'mysql', name: 'mysql-connector-java', version: mysql_connector_java_version

    // Test
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertj_core_version
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: spring_boot_version
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: spring_boot_version
}

configurations {
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'org.slf4j', module: 'slf4j-jdk14'
    all*.exclude group: 'org.slf4j', module: 'slf4j-simple'
    all*.exclude group: 'org.jadira.usertype', module: 'usertype.jodatime'
}